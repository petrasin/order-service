plugins {
  id 'java'
  id 'org.springframework.boot' version '2.7.11'
  id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

description = 'Provides functionality for purchasing books.'

repositories {
  mavenCentral()
}

ext {
  set('springCloudVersion', "2021.0.3")
  set('testcontainersVersion', "1.17.6")
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.cloud:spring-cloud-starter-config'
  implementation 'org.springframework.cloud:spring-cloud-stream-binder-rabbit'

  runtimeOnly 'org.postgresql:postgresql'
  runtimeOnly 'org.postgresql:r2dbc-postgresql'
  runtimeOnly 'org.flywaydb:flyway-core'
  runtimeOnly 'org.springframework:spring-jdbc'
  runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.79.Final:osx-aarch_64'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'io.projectreactor:reactor-test'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:postgresql'
  testImplementation 'org.testcontainers:r2dbc'
  testImplementation 'com.github.dasniko:testcontainers-keycloak:2.3.0'
  testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  testImplementation("org.springframework.cloud:spring-cloud-stream") {
    artifact {
      name = "spring-cloud-stream"
      extension = "jar"
      type ="test-jar"
      classifier = "test-binder"
    }
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
  }
}

tasks.named('test') {
  useJUnitPlatform()
}

bootBuildImage {
  imageName = "${project.name}"
  environment = ["BP_JVM_VERSION": "17.*"]
}
